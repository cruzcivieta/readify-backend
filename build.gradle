plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.61"
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.50'
}

group 'com.readify'
version '1.0-SNAPSHOT'

apply plugin: 'application'

sourceSets {
    main {
        java { srcDirs = ['api/main/kotlin'] }
        resources { srcDirs = ['api/main/kotlin/resources'] }
    }

    test {
        java { srcDirs = ['api/test/kotlin'] }
        resources { srcDirs = ['api/test/kotlin/resources'] }
    }
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-jpa'
    apply plugin: 'kotlin-spring'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation 'io.github.microutils:kotlin-logging:1.7.8'
        implementation 'org.springframework.boot:spring-boot-starter-web:2.2.2.RELEASE'

        // Common dependencies for testing
        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
        testImplementation 'io.mockk:mockk:1.9.3'
        testImplementation 'com.willowtreeapps.assertk:assertk-jvm:0.21'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

subprojects {
    group = "com.readify.${rootProject.name}"
    sourceSets {
        main {
            java { srcDirs = ['main/kotlin'] }
            resources { srcDirs = ['main/kotlin/resources'] }
        }

        test {
            java { srcDirs = ['test/kotlin'] }
            resources { srcDirs = ['test/kotlin/resources'] }
        }
    }

    dependencies {
        if (project.name != "shared") {
            implementation project(":shared")
            testImplementation project(":shared").sourceSets.test.output
        }
    }
}

dependencies {
    // Bounded contexts
    implementation project(":search")
    implementation project(":userprofile")
    implementation project(":authentication")

    // Shared kernel
    implementation project(":shared")

    // Dependencies
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.2.2.RELEASE'
    implementation 'javax.persistence:javax.persistence-api:2.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.2.4.RELEASE'
    implementation 'org.postgresql:postgresql:42.2.5'

    // Swagger
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    // Test
    testImplementation 'io.rest-assured:rest-assured:4.2.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.2.RELEASE'
    testImplementation 'com.ninja-squad:springmockk:2.0.0'
}

mainClassName = 'com.readify.api.Application'